plugins {
    id 'org.jetbrains.kotlin.js' version '1.6.10'
    id "io.gitlab.arturbosch.detekt" version '1.19.0'
}

repositories {
    mavenCentral()
    maven {
        url 'https://maven.pkg.jetbrains.space/public/p/kotlinx-html/maven'
    }
}

dependencies {
    testImplementation 'org.jetbrains.kotlin:kotlin-test-js'
    implementation 'org.jetbrains.kotlinx:kotlinx-html-js:0.7.2'
}

kotlin {
    js {
        browser {
            binaries.executable()
            distribution {
                directory = file("${projectDir.parentFile.toPath()}/backend/src/main/resources/public")
            }
            webpackTask {
                cssSupport.enabled = true
            }
            runTask {
                cssSupport.enabled = true
            }
            testTask {
                useKarma {
                    useChromeHeadless()
                    webpackConfig.cssSupport.enabled = true
                }
            }
        }
    }
}

detekt {
    ignoreFailures = false
    config = files("$projectDir/config/detekt.yml")
    input = objects.fileCollection().from(
        "src/main/kotlin",
        "src/test/kotlin"
    )
}

tasks.named("detekt").configure {
    reports {
        // Enable/Disable XML report (default: true)
        xml.required.set(true)
        xml.outputLocation.set(file("build/reports/detekt.xml"))

        // Enable/Disable HTML report (default: true)
        html.required.set(true)
        html.outputLocation.set(file("build/reports/detekt.html"))

        // Enable/Disable TXT report (default: true)
        txt.required.set(true)
        txt.outputLocation.set(file("build/reports/detekt.txt"))

        // Enable/Disable SARIF report (default: false)
        sarif.required.set(true)
        sarif.outputLocation.set(file("build/reports/detekt.sarif"))
    }
}
